@start
  *say
    Welcome to the Alexa Presentation Language Demo for Skill Flow Builder. 
    [pause]    
    You can now easily add visuals to your Alexa Skill using APL templates, custom commands, and animations. 
    [pause]
    In addition, the dynamic pager system makes it easy to create slide shows using built in layouts, as well as mix and match custom layouts that you create. 
    [pause]
    Are you ready to hear more?
  *reprompt
    Are you ready to hear more?
  *show
    template: 'title-screen-template'

  *then
    hear yes, continue, ready, i am ready, let's go, sure {
      -> templates    
    }
    hear no, nope, stop, no thank you {
      -> end
    }
    
@templates
  *say
    Great! Starting with a simple scene in skill flow builder,
    [pause]
  *show
    template: 'commands-demo-template'
    showCommandsTitle
    codeAppear4
  *then
    -> commands

@commands
  *say
    You can add a *show section to any scene to add visuals to that scene.  
    [pause]
  *show
    codeAppear3
  *then
    -> commands-0

@commands-0
  *say
    Further show sections can invoke commands on your loaded APL file,
    [pause]
  *show
    codeAppear1
  *then
    -> commands-1

@commands-1
  *say
    to trigger actions, such as making an image appear on the screen.
  *show
    diceAppear
  *then
    -> commands-2
  
@commands-2
  *say
    Or starting animations to bring your screens to life.
    [pause]
    Are you ready to hear more?
  *reprompt
    Are you ready to hear more?
  *show
    diceSpin
  *then
    hear yes, continue, ready, i am ready, let's go, sure {
      -> dynamic-pager    
    }
    hear no, nope, stop, no thank you {
      -> end
    }

@dynamic-pager
  *say
    To make life easier, the dynamic pager template allows you to create a slideshow out of a collection of layouts,
  *show
    template: 'dynamic-pager'
    layout: 'BackgroundLayout'
    title: 'Using Dynamic Pager'
  *then
    -> dynamic-pager-0

@dynamic-pager-0
  *say
    allowing you to easily customize the look and feel of each scene.
  *show
    template: 'dynamic-pager'
    layout: 'BackgroundLayout'
    title: 'BackgroundLayout'
    background: 'https://sfb-framework.s3.amazonaws.com/examples/images/green-plant.jpg'
  *then
    -> dynamic-pager-1

@dynamic-pager-1
  *say
    The first example layout is called the Background Layout, you can use it to display full screen images.
  *show
    template: 'dynamic-pager'
    layout: 'BackgroundLayout'
    title: 'BackgroundLayout'
    background: 'https://sfb-framework.s3.amazonaws.com/examples/images/blue-sea.jpg'
  *then
    -> dynamic-pager-2

@dynamic-pager-2
  *say
    And intermix spoken text along with the slide show.
  *show
    template: 'dynamic-pager'
    layout: 'BackgroundLayout'
    title: 'BackgroundLayout'
    background: 'https://sfb-framework.s3.amazonaws.com/examples/images/red-dots.jpg'
  *then
    -> sequence
    
@sequence
  *say
    Next is the Sequence layout, used for listing items, such as for a shop in the game, or actions to take. The user can interact via voice or by touch events. 
  *show
    template: 'dynamic-pager'
    layout: 'DiceSequenceLayout'
    title: 'SequenceLayout'
    items: [{'name':'Item 1'},{'name':'Item 2'},{'name':'Item 3'}]
  *then
    -> sequence-1

@sequence-1
  *say
    Or create your own layouts to customize your visuals. Like making a dice roll in your game.
  *show
    template: 'dynamic-pager'
    layout: 'DiceRollAutoLayout'
    title: 'CustomLayout'
    background: 'https://sfb-framework.s3.amazonaws.com/examples/images/dark-background.jpg'
    color: 'blue'
    D: 20
    roll: 17
  *then
    -> sequence-2

@sequence-2
  *say
    <audio src='https://sfb-framework.s3.amazonaws.com/examples/sounds/dice_roll_sfx.mp3' />
    <break time='3.0s'/>
  *show
    rollDice
  *then
    -> sequence-3-0

@sequence-3-0
  *show
    fadeDiceOut
  *then
    -> sequence-3

@sequence-3
  *say
    Using APL with Skill Flow Builder is easy, that's it for now.

    I'll leave you with a dice demo just for fun.
  *show
    template: 'dynamic-pager'
    layout: 'DiceSequenceLayout'
    background: 'https://sfb-framework.s3.amazonaws.com/examples/images/board-game-sm-white.jpg'
  *then
    flag firstTime
    -> dice-menu

@dice-menu
  *say
    Please choose from one of the dice shown on the screen, or touch them to select.
  *reprompt
    Please choose from one of the dice shown on the screen, or touch them to select.
  *show
    template: 'dynamic-pager'
    layout: 'DiceSequenceLayout'
    background: 'https://sfb-framework.s3.amazonaws.com/examples/images/board-game-sm-white.jpg'
  *then
	  slot num_sides to 'AMAZON.NUMBER'

    hear roll a {num_sides} sided die, roll a {num_sides} sided dice, {num_sides} sided, {num_sides} sides, {num_sides} {
        set number_of_sides to num_sides
        -> dice-roll
    }

    hear roll a D4, D4 {
        set number_of_sides to 4
        -> dice-roll
    }

    hear roll a D6, D6 {
        set number_of_sides to 6
        -> dice-roll
    }

    hear roll a D20, D20 {
        set number_of_sides to 20
        -> dice-roll
    }

    hear no, nope, stop, no thank you {
      -> end
    }

@dice-roll
  *then
  	roll 1d{number_of_sides}
  	set roll_value to rollResult
    -> dice-roll-1

@dice-roll-1
  *say
    Rolling a {number_of_sides} sided die.
  *show
    template: 'dynamic-pager'
    layout: 'DiceRollAutoLayout'
    background: 'https://sfb-framework.s3.amazonaws.com/examples/images/board-game-sm-white.jpg'
    D: '{number_of_sides}'
    roll: '{roll_value}'
  *then
    -> dice-roll-1-2

@dice-roll-1-2
  *say
    <audio src='https://sfb-framework.s3.amazonaws.com/examples/sounds/dice_roll_sfx.mp3' />
  *show
    rollDice
  *then
    -> dice-roll-2

@dice-roll-2
  *say
    You got a {roll_value}.
  *then
    -> dice-roll-3

@dice-roll-3
  *show
    fadeDiceOut
  *then
    if firstTime {
      unflag firstTime
      <-> say-end-of-demo-message
    }
    -> dice-menu *reprompt

@resume
*then
  >> RESTART

@say-end-of-demo-message
  *say
    That's the end of the demo, you can keep rolling for fun.
  *then
    >> RETURN
        
@end 
  *say
    Thank you, goodbye.
    


